#include "/home/terra/TerraNG/terra/TerraNet.defs"
#define FIRST_MESSAGE 1
#define DATA_MESSAGE 2

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var usrMsg tempMsg;
var usrMsg forwardMsg;
var usrMsg msgRadio;
var usrMsg firstMsg;

var ushort noPai = 0;
var ubyte stat;

var ubyte espera = 100;
var ubyte salto = 0;

tempMsg.type = DATA_MESSAGE;

firstMsg.target = BROADCAST;
firstMsg.type = FIRST_MESSAGE;

if nodeId == 11 then
	await 1s;
	// enviar inicio do broadcast 
	firstMsg.source = nodeId;
	salto = 1;
	firstMsg.d8[0] = salto;
	noPai = 1;
else
	// esperar o broadcast e salvar o pai, enviar um novo broadcast com
    // source mudado
	firstMsg = await RECEIVE(FIRST_MESSAGE);
	noPai = firstMsg.source;
	firstMsg.source = nodeId;
	salto = firstMsg.d8[0] + 1;
	firstMsg.d8[0] = salto;
end

espera = espera * salto;
firstMsg.d8[1] = 0;
emit SEND(firstMsg);
await SEND_DONE;	

par do
	await (espera)ms;
	loop do
		tempMsg.source = nodeId;
		tempMsg.d8[1] = tempMsg.d8[1] + 1;
		if nodeId == 11 then
			tempMsg.target = 1;
		else
			tempMsg.target = noPai;
		end
		await 10s;
		emit REQ_TEMP();
		tempMsg.d16[0] = await TEMP;
		stat = qPut(tempMsg);
	end
with
	loop do
		forwardMsg = await RECEIVE(DATA_MESSAGE);
		forwardMsg.target = noPai;
		stat = qPut(forwardMsg);
	end
with
	loop do
		await Q_READY;
		loop do
			stat = qGet(msgRadio);
			emit SEND_ACK(msgRadio);
			await SEND_DONE_ACK;
			await 100ms;
			if qSize() == 0 then
				break;
			end
		end
	end
end

