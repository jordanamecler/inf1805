#include "/home/terra/TerraNG/terra/TerraNet.defs"
#define FIRST_MESSAGE 1
#define DATA_MESSAGE 2

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var usrMsg tempMsg;
var usrMsg forwardMsg;
var usrMsg msgRadio;
var usrMsg firstMsg;

var ushort noPai = 0;
var ubyte stat;

var ubyte espera = 50;
var ubyte salto = 0;

tempMsg.type = DATA_MESSAGE;

firstMsg.target = BROADCAST;
firstMsg.type = FIRST_MESSAGE;

if nodeId == 11 then
	await 1s;
	// enviar inicio do broadcast 
	firstMsg.source = nodeId;
	firstMsg.d8[0] = 1;
	noPai = 1;
else
	// esperar o broadcast e salvar o pai, enviar um novo broadcast com
    	// source mudado
	firstMsg = await RECEIVE(FIRST_MESSAGE);
	noPai = firstMsg.source;
	firstMsg.source = nodeId;
	// a cada 'camada' de nos adiciona 1 pra enviarem em tempos diferentes
	firstMsg.d8[0] = firstMsg.d8[0] + 1;
end

// espera varia entre 50, 100, 150, 200... por camada 
espera = espera + (50*(firstMsg.d8[0]));
firstMsg.d8[1] = 0;
emit SEND(firstMsg);
await SEND_DONE;	

par do
	// mensagem de temperatura
	await 1s;
	loop do
		tempMsg.source = nodeId;
                // definicao do round
		tempMsg.d8[1] = tempMsg.d8[1] + 1;
		if nodeId == 11 then
			tempMsg.target = 1;
		else
			tempMsg.target = noPai;
		end
		await 10s;
		emit REQ_TEMP();
		tempMsg.d16[0] = await TEMP;
		stat = qPut(tempMsg);
		await (espera)ms;
	end
with
	// mensagem do filho
	loop do
		forwardMsg = await RECEIVE(DATA_MESSAGE);
		forwardMsg.target = noPai;
		stat = qPut(forwardMsg);
	end
with
	// envia mensagens da pilha
	loop do
		await Q_READY;
		loop do
			stat = qGet(msgRadio);
			emit SEND_ACK(msgRadio);
			await SEND_DONE_ACK;
			await 50ms;
			if qSize() == 0 then
				break;
			end
		end
	end
end

