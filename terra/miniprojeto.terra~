#include "/home/terra/TerraNG/terra/TerraNet.defs"
#define FIRST_MESSAGE 1
#define DATA_MESSAGE 2

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2] d32;
end

var usrMsg tempMsg;
var usrMsg forwardMsg;
var usrMsg msgRadio;
var usrMsg firstMsg;

var ushort noPai = 0;
var ubyte stat;

tempMsg.type = DATA_MESSAGE;

firstMsg.target = BROADCAST;
firstMsg.type = FIRST_MESSAGE;

if nodeId == 11 then
	// enviar inicio do broadcast 
	firstMsg.source = nodeId;
	noPai = 1;
else
	// esperar o broadcast e salvar o pai, enviar um novo broadcast com
    // source mudado
	firstMsg = await RECEIVE(FIRST_MESSAGE);
	noPai = firstMsg.source;
	firstMsg.source = nodeId;
end

emit SEND(firstMsg);
await SEND_DONE;	

par do
	loop do
		tempMsg.source = nodeId;
		if nodeId == 11 then
			tempMsg.target = 1;
		else
			tempMsg.target = noPai;
		end
		await 10s;
		emit REQ_TEMP();
		tempMsg.d16[0] = await TEMP;
		stat = qPut(tempMsg);
	end
with
	loop do
		forwardMsg = await RECEIVE(DATA_MESSAGE);
		forwardMsg.target = noPai;
		stat = qPut(forwardMsg);
	end
with
	loop do
		await Q_READY;
		loop do
			stat = qGet(msgRadio);
			emit SEND(msgRadio);
			await SEND_DONE;
			if qSize() == 0 then
				break;
			end
		end
	end
end

